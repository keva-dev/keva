plugins {
    id 'application'
    id 'com.adarshr.test-logger' version '2.1.0'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation project(':core')
    implementation project(':config')

    implementation 'ch.qos.logback:logback-classic:1.2.7'
    implementation 'ch.qos.logback:logback-core:1.2.7'

    testImplementation 'redis.clients:jedis:3.7.0'
}

def mainClass = 'dev.keva.app.Application'

jar {
    manifest {
        attributes(
                'Main-Class': "${mainClass}",
        )
    }
}

task fatJar(type: Jar) {
    dependsOn(':config:build')
    dependsOn(':util:build')
    dependsOn(':resp-protocol:build')
    dependsOn(':storage:build')
    dependsOn(':core:build')
    manifest.from jar.manifest
    classifier = 'all'
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    } {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }
    with jar
}

artifacts {
    archives fatJar
}

application {
    mainClassName = mainClass
}

test {
    useJUnitPlatform()
    testLogging {
        outputs.upToDateWhen { true }
        showStandardStreams = true
    }
    maxParallelForks = Runtime.runtime.availableProcessors()
}
