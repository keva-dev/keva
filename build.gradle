plugins {
    id "org.sonarqube" version "3.1.1"
}

subprojects {
    apply plugin: 'idea'
    apply plugin: 'java-library'
    apply plugin: 'signing'
    apply plugin: 'maven-publish'
    apply plugin: 'jacoco'

    group 'com.jinyframework'
    version '0.3.3'

    repositories {
        mavenCentral()
    }

    dependencies {
        // Lombok
        compileOnly 'org.projectlombok:lombok:1.18.12'
        annotationProcessor 'org.projectlombok:lombok:1.18.12'
        testCompileOnly 'org.projectlombok:lombok:1.18.12'
        testAnnotationProcessor 'org.projectlombok:lombok:1.18.12'

        // JUnit
        testImplementation('org.junit.jupiter:junit-jupiter:5.6.2')

        // SLF4J as a facade
        compile group: 'org.slf4j', name:'slf4j-api', version: '1.7.2'
    }

    publishing {
        repositories {
            maven {
                def releasesRepoUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
                def snapshotsRepoUrl = 'https://oss.sonatype.org/content/repositories/snapshots'
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                credentials {
                    username = project.findProperty("sonatypeUsername") ?: System.getenv("sonatypeUsernamer")
                    password = project.findProperty("sonatypePassword") ?: System.getenv("sonatypePassword")
                }
            }
        }
    }

    jacocoTestReport {
        getExecutionData().setFrom(fileTree(buildDir).include("/jacoco/*.exec"))
        reports {
            xml.enabled true
        }
    }

    test {
        finalizedBy jacocoTestReport
    }
}

sonarqube {
    properties {
        property "sonar.projectKey", "tuhuynh27_keva"
        property "sonar.organization", "huynhminhtufu-github"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}
